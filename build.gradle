plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'io.openapiprocessor.openapi-processor'  version '2022.1'
    id "io.freefair.lombok" version "6.5.0.2"
    id 'jacoco'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.verint.tasksapi.ToDoApiApplication'
        )
    }
}

group = 'com.verint'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.mapstruct.extensions.spring:mapstruct-spring-annotations:0.1.1'
    implementation 'org.mapstruct:mapstruct:1.5.2.Final'

    runtimeOnly 'com.h2database:h2'

    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mapstruct.extensions.spring:mapstruct-spring-annotations:0.1.1'
    testImplementation 'org.mapstruct:mapstruct:1.5.2.Final'

    testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.5.2.Final"

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 1.0
            }
            excludes = [
                    'com.verint.todoappapi.ToDoAppApiApplication',
                    'com.verint.todoappapi.ToDoConfig',
            ]
        }
    }
}

openapiProcessor {
    spring {
        processor 'io.openapiprocessor:openapi-processor-spring:2022.3'
        apiPath "$projectDir/src/main/resources/to-do-openapi.yaml"
        targetDir "$projectDir/build/openapi"
        mapping "$projectDir/src/main/resources/mapping.yaml"
        showWarnings true
    }
}

sourceSets {
    main {
        java {
            srcDirs "$projectDir/build/openapi"
            srcDirs "$projectDir/build/generated/sources"
        }
    }
}

compileJava.dependsOn('processSpring')
build.dependsOn jacocoTestCoverageVerification